import org.apache.tools.ant.taskdefs.condition.Os

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.toolboxVersion = "1.0.17"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url = uri("https://maven.pkg.github.com/hyperether/android-toolbox")

            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "maven-publish"

    afterEvaluate {
        // Filter projects, exclude applications and include libraries
        if (!plugins.hasPlugin("android") && plugins.hasPlugin("android-library")) {
            // Run assembleRelease task before running publish task
            tasks.whenTaskAdded { publishTask ->
                if (publishTask.name == 'publishToolboxToHyperEtherRepository') {
                    publishTask.dependsOn 'assembleRelease'
                }
            }

            task sourceJar(type: Jar) {
                from android.sourceSets.main.java.srcDirs
                classifier "sources"
            }

            publishing {
                publications {
                    hyperether(MavenPublication) {
                        groupId "com.hyperether"
                        artifactId "android-toolbox"
                        version toolboxVersion
                        artifact(sourceJar)
                        artifact("$buildDir/outputs/aar/android-toolbox-release.aar")

                        pom.withXml {
                            def dependenciesNode = asNode().appendNode('dependencies')

                            //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                            configurations.api.allDependencies.each {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }

                repositories {
                    maven {
                        name = "hyperether"
                        url = uri("https://maven.pkg.github.com/hyperether/android-toolbox")
                        credentials {
                            username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                            password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                        }
                    }
                }
            }

        }
    }
    tasks.withType(Javadoc).all { enabled = false }
}
